# Auto-completion
# ---------------
if [[ $- == *i* ]]; then
    if [[ -f "/usr/local/opt/fzf/shell/completion.zsh" ]]; then
        source "/usr/local/opt/fzf/shell/completion.zsh" 2> /dev/null
    elif [[ -f "/usr/share/fzf/completion.zsh" ]]; then
        source "/usr/share/fzf/completion.zsh" 2> /dev/null
    fi
fi

# repeat history
function fzf-history() {
    print -z $( ([ -n "$ZSH_NAME" ] && fc -l 1 || history) |
        fzf-tmux --prompt="History> " +s --tac |
        sed 's/ *[0-9]* *//')
}

# CTRL-R - Paste the selected command from history into the command line
function fzf-history-widget() {
    local tac=${commands[tac]:-"tail -r"}
    BUFFER=$( ([ -n "$ZSH_NAME" ] && fc -l 1 || history) |
        sed 's/ *[0-9]*\** *//' |
        eval $tac | awk '!a[$0]++' |
        fzf-tmux --prompt="History> " +s)
    CURSOR=$#BUFFER
    zle clear-screen
}
zle     -N   fzf-history-widget
bindkey '^R' fzf-history-widget

# fbr - checkout git branch
fbr() {
    local branches branch
    branches=$(git branch -vv) &&
        branch=$(echo "$branches" | fzf-tmux --prompt="Branch> " +m) &&
        git checkout $(echo "$branch" | awk '{print $1}' | sed "s/.* //")
}

# fco - checkout git branch/tag
fco() {
    local tags branches target
    tags=$(
    git tag | awk '{print "\x1b[31;1mtag\x1b[m\t" $1}') || return
    branches=$(
    git branch --all | grep -v HEAD             |
    sed "s/.* //"    | sed "s#remotes/[^/]*/##" |
    sort -u          | awk '{print "\x1b[34;1mbranch\x1b[m\t" $1}') || return
    target=$(
    (echo "$tags"; echo "$branches") |
    fzf-tmux --prompt="Checkout> " -l30 -- --no-hscroll --ansi +m -d "\t" -n 2) || return
    git checkout $(echo "$target" | awk '{print $2}')
}

# fcs - get git commit sha
# example usage: git rebase -i `fcs`
fcs() {
    local commits commit
    commits=$(git log --color=always --pretty=oneline --abbrev-commit --reverse) &&
        commit=$(echo "$commits" | fzf-tmux --tac +s +m -e --ansi --reverse) &&
        echo -n $(echo "$commit" | sed "s/ .*//")
}

# fstash - easier way to deal with stashes
# type fstash to get a list of your stashes
# enter shows you the contents of the stash
# ctrl-d shows a diff of the stash against your current HEAD
# ctrl-b checks the stash out as a branch, for easier merging
fstash() {
    local out q k sha
    while out=$(
        git stash list --pretty="%C(yellow)%h %>(14)%Cgreen%cr %C(blue)%gs" |
        fzf-tmux --ansi --no-sort --query="$q" --print-query \
            --expect=ctrl-d,ctrl-b);
    do
        mapfile -t out <<< "$out"
        q="${out[0]}"
        k="${out[1]}"
        sha="${out[-1]}"
        sha="${sha%% *}"
        [[ -z "$sha" ]] && continue
        if [[ "$k" == 'ctrl-d' ]]; then
            git diff $sha
        elif [[ "$k" == 'ctrl-b' ]]; then
            git stash branch "stash-$sha" $sha
            break;
        else
            git stash show -p $sha
        fi
    done
}

# change directory
fd() {
    # sed置換用に/をエスケープ
    local home_esc=$(echo $HOME | sed 's,\/,\\\/,g')

    local dir
    dir=$( ((cdr -l | awk '{ print $2 }') ;
        find -L ${1:-$(pwd)} -type d -maxdepth 3 -print 2> /dev/null) |
        sed "s/\~/$home_esc/" |
        awk '!a[$0]++' |
        fzf --prompt="cd > " +m)

    if [ -n "$dir" ]; then
        cd $dir
    fi
}


# cdr
fdr () {
    local selected_dir=$(cdr -l |
        awk '{ print $2 }' |
        fzf-tmux --prompt="cd> ")
    selected_dir=${selected_dir/#\~/$HOME}
    if [ -n "$selected_dir" ]; then
        cd $selected_dir
    fi
}

# ghq
function fhq {
    local dir="$(ghq list -p | fzf-tmux --prompt="ghq> ")"
    if [ -n "$dir" ] ; then
        cd "$dir"
    fi
}

# bookmark
function bd()
{
    local dir="$(cat ~/.bookmarks |
        fzf --prompt="cd> " |
        awk '{print $2}')"
    if [ -n "$dir" ] ; then
        cd "$dir"
    fi
}
